pluginManagement {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        // https://docs.gradle.org/current/userguide/declaring_repositories.html#declaring_content_exclusively_found_in_one_repository
        exclusiveContent {
            forRepository {
                maven {
                    name = "Sponge"
                    url = "https://repo.spongepowered.org/repository/maven-public"
                }
            }
            filter {
                includeGroupAndSubgroups("org.spongepowered")
            }
        }
        exclusiveContent {
            forRepositories(
                maven {
                    name = "ParchmentMC"
                    url = "https://maven.parchmentmc.org/"
                },
                maven {
                    name = "NeoForge"
                    url = "https://maven.neoforged.net/releases"
                }
            )
            filter {
                includeGroup("org.parchmentmc.data")
            }
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Fabric'
                    url = uri('https://maven.fabricmc.net')
                }
            }
            filter {
                includeGroup('net.fabricmc')
                includeGroup('fabric-loom')
            }
        }
    }
}

plugins {
    // https://plugins.gradle.org/plugin/org.gradle.toolchains.foojay-resolver-convention
    id "org.gradle.toolchains.foojay-resolver-convention" version "0.8.0"
}

// This should match the folder name of the project, else IDEA may complain (https://youtrack.jetbrains.com/issue/IDEA-317606)
rootProject.name = "Framework"
include("common")
include("fabric")
include("neoforge")
